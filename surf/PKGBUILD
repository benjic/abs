# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Pierre Chapuis <catwell at archlinux dot us>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Michael Witten <mfwitten>
# Contributor: Raphael Proust <raphlalou at gmail dot com>

pkgname=surf
pkgver=2.0
pkgrel=1
pkgdesc='A simple web browser based on WebKit/GTK+.'
arch=('i686' 'x86_64')
url='http://surf.suckless.org/'
license=('MIT')
depends=('webkit2gtk' 'xorg-xprop')
optdepends=('dmenu: URL-bar'
            'ca-certificates: SSL verification'
            'xterm: default download handler'
            'curl: default download handler')
source=(
  'http://dl.suckless.org/surf/surf-2.0.tar.gz'
  'homepage.patch'
  'webkit-parameter.patch'
)
md5sums=(
  '11713901fa83c536f3ddfacfc28c3acc'
  '17cd6ee0b3d315d17d516929bb1e0c8d'
  '5c9c07e5668e175dc2f2077f233f72a3'
)
groups=('modified')


build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 -i "$srcdir/homepage.patch"
  patch -p0 -i "$srcdir/webkit-parameter.patch"
}
