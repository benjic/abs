# $Id: PKGBUILD 160802 2016-02-08 01:07:39Z foutrelis $
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: totoloco <totoloco at gmx>

pkgname=tabbed
pkgver=0.6
pkgrel=2
pkgdesc='Simple generic tabbed fronted to xembed aware applications.'
arch=('i686' 'x86_64')
url='http://tools.suckless.org/tabbed'
license=('custom:MIT/X')
depends=('libx11')
source=("http://dl.suckless.org/tools/${pkgname}-${pkgver}.tar.gz"
        'config.def.h.patch')
md5sums=('7898992feb9358e0196119750b77b42c'
         '11fa372cea8b2f2b2110bfbafc843837')
groups=('modified')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 -i "$srcdir/config.def.h.patch"
}
